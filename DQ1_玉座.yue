import 'macros' as {$}
import 'utl' as :L, :play, :toZenkaku
import 'lib.lume'
import 'ui' as :Scene, :BGM
import 'main' as :Castle, :Charactor, :MessageBoxRetro, :SelectBoxRetro, :g_player, :宝箱, :扉, :LevelUps, :play_wait
local *


_ = $mml2ogg([[
Tempo(128)
TR(1)
@8o5l8 redece<b>e<a2+4r4>
r'fd''ec''fd'd'fd'c'fd'<b2+4r4>
rgfgegc#g f4g4a4gf
e4ced4e-4e1
l16 [2 r8ag#a8ee-e8c<b>c8<a8> ]
l8 <bg>c<g>f<a>c<a >e<eg#b>e<g>c#e
l16 [2 r8ag#a8fef8dc#d8<a8> ]
l8 <bg>c<g>f<a>cf e<'fa''eg#''df''ce''<b>d''<a>c''<gb>'>
TR(2)v(75)
@8l4o3  arrrl8ra>ceagfe
l4drba l8g#eg#bSub{>e4<}ref#g#
a>dedc#e<a>e d<a>e<a>f<a>ed
c<g>e<gb>f<a>f#g#<eg#b>edc<b
l16 a8>c<b>c8c<b>'c<f#>'8ee-e8c8<
    g8>c<b>c8c<b>'c<f#>'8ee-e8c8<
l4 fede-e2a2
l16 d8>fec8dc#d8fef8d8<
    >c8fec8dc#d8fef8d8<
l8 fed2 e4r4+2 ]], castle, 30.0)


mapdata = $to2d[[
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@CCCCCCCCCC@@@@@@@@
@@@@@@@@C''''''''C@@@@@@@@
@@@@@@@@C'AAAAAA'C@@@@@@@@
@@@@@@@@C'A'AA'A'C@@@@@@@@
@@@@@@@@C''''''''C@@@@@@@@
@@@@@@@@C''''''''C@@@@@@@@
@@@@@@@@C''''''''C@@@@@@@@
@@@@@@@@CCCC'CCCCC@@@@@@@@
@@@@@@@@C'''''''4C@@@@@@@@
@@@@@@@@CCCCCCCCCC@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@]]


-- buildを呼ぶとシーン(シナリオ)を作る
-- いくつかのbuild_xxx()を１ファイルに入れる。全体的に別セリフだったり、小さなマップが合ったり
export build_fromDownstair = ()->
	with Castle(mapdata)
		deployローラ姫 = ->
			.charactors[] = with Charactor(14, 12, {0x32}) -- ローラ姫
				.talk = => MessageBoxRetro({ <index>: coroutine.wrap((_, _)->
					coroutine.yield 1, L('ローラ「%sさまは　ローラのこと\n　　　　を　おもってくださいますか？')\format(g_player.name)
					sb = SelectBoxRetro({L'はい', L'いいえ'})
					coroutine.yield 1, sb\attach()
					coroutine.yield 1, L('\nローラ「うれしゅうごさいます。ぽっ')
					BGM()
					play_wait'assets/sounds/mml2ogg/ローラ姫救出.ogg'
					BGM Castle.BGM
					nil
				)})\attach()
		.charactors[] = with Charactor(11,12,{0x30}) -- 王様
			.talk = =>
				if not lume.find(g_player.flags, L'ローラ姫を救出した')
					MessageBoxRetro({
						L('＊「おお　%s！\n　　よくぞ　もどってきた！\n　　わしは　とても　うれしいぞ。🔻')\format(g_player.name)
						if g_player.level < 30
							L('\n＊「そなたが　つぎのレベルになるには\n　　あと%sポイントの\n　　けいけんがひつようじゃ。🔻')\format(toZenkaku(LevelUps[g_player.level+1].経験値 - g_player.経験値))
						else
							L('\n＊「そなたは　もう　じゅうぶんに\n　　つよい！　なぜに　まだ\n　　りゅうおうを　たおせぬのか？🔻')
						--冒険の書の下りさくじょ
						L('\n＊「では　また　あおう\n　　ゆうしゃ　%sよ！🔻')\format(g_player.name)})\attach()
				else
					MessageBoxRetro({ <index>: coroutine.wrap((_, _)->
						coroutine.yield 1, L('＊「おお　%s！　よくぞ\n　　ひめを　たすけだしてくれた。\n　　こころから　れいをいうぞ！🔻')\format(g_player.name)
						coroutine.yield 1, L('\n＊「さあ　ローラ\n　　わたしのとなりへ。🔻')
						coroutine.yield 1, L('\nローラ「まってください。　ローラは\n　　　　%sさまに　おくりものを\n　　　　しとうございます。🔻')\format(g_player.name)
						coroutine.yield 1, L('\nローラ「%sを　あいする\n　　　　わたしの　こころ。どうか\n　　　　うけとってくださいませ。🔻')\format(g_player.name)
						coroutine.yield 1, L('\nローラ「ああ！　たとえ　はなれて\n　　　　いても　ローラは　いつも\n　　　　あなたと　ともに　あります。🔻')
						coroutine.yield 1, L('\nローラ「では、%sさま……🔻')\format(g_player.name)
						deployローラ姫()
						lume.remove(g_player.flags, L'ローラ姫を救出した')
						g_player.items[] = L'おうじょのあい'
						g_player.chipids = if g_player.武器 and g_player.盾 then {0x26, 0x27}elseif g_player.盾 then {0x24, 0x25} elseif g_player.武器 then {0x22, 0x23} else {0x20, 0x21}
						--冒険の書の下りさくじょ
						coroutine.yield 1, L('\n＊「では　また　あおう\n　　ゆうしゃ　%sよ！🔻')\format(g_player.name)
						nil
					)})\attach()
		if lume.find(g_player.items, L'おうじょのあい') then deployローラ姫()
		.charactors[] = with Charactor(11,15,{0x34,0x35}) -- 兵士
			.talk = => MessageBoxRetro({
				L'＊「おかねが　たまったら\n　　ぶきやぼうぐを　たかいものに\n　　かいかえることです。🔻'
				L'\n＊「そうするえば　もっと\n　　つよくなれるでしょう。🔻'})\attach()
		.charactors[] = with Charactor(13,15,{0x34,0x35}) -- 兵士
			.talk = => MessageBoxRetro({
				L'＊「ひとびとのはなにしは　よく\n　　みみをかたむけることです。\n　　きっと　やくにたつでしょう🔻'})\attach()
		.charactors[] = with Charactor(15,13,{0x34,0x35}) -- 兵士
			.update = coroutine.wrap(._update_randomwalk)
			.talk = =>
				sb1 = SelectBoxRetro({L'はい', L'いいえ'})
				MessageBoxRetro({ <index>: coroutine.wrap((_, _)->
					coroutine.yield 1, L'＊「ローラひめのことを　ごぞんじか？'
					coroutine.yield 1, sb1\attach() -- 次の２行をスキップ
					if 2 == sb1.index
						coroutine.yield 1, L'\n＊「ひめさまが　まものたちに\n　　さらわれて　はんとしになる…🔻'
						coroutine.yield 1, L'\n＊「おうさまは　なにも\n　　おっしゃらないが\n　　とても　くるしんでいるはず。🔻'
					coroutine.yield 1, L('\n＊「%s！\n　　どうか　ひめを\n　　たすけだして　ください！🔻')\format(g_player.name)
					nil
				)})\attach()
		.traps[] = with {16, 17} --階段
			.talk = =>
				play'assets/DQ1/階段.ogg'
				Scene.current\blackout -> Scene.current = require('DQ1_ラダトーム城').build_from玉座()

		BGM Castle.BGM
		g_player.x, g_player.y = 16, 17


export build_死に戻り = ()->
	with Castle(mapdata)
		.charactors[] = with Charactor(11,12,{0x30}) -- 王様
			.talk = =>
				if not lume.find(g_player.flags, L'呪い')
					MessageBoxRetro({
						L('＊「おお　%s！\n　　しんでしまうとは　なにごとだ！🔻\n　　しかたのない　やつだな。🔻')\format(g_player.name)
						L'\n＊「おまえに　もう　いちど\n　　きかいを　あたえよう！🔻'
						L'\n＊「たたかいで　キズついたときは\n　　まちにもどり　やどやに　とまって\n　　キズをかいふくさせるのだぞ。🔻'
						L'\n＊「ふたたび　このようなことが\n　　おこらぬことを\n　　わしは　いのっている！🔻'
						-- L('\n＊「そなたが　つぎのレベルになるには\n　　あと%sポイントの\n　　けいけんがひつようじゃ。🔻')\format(toZenkaku(LevelUps[g_player.level+1].経験値 - g_player.経験値))
						if g_player.level < 30
							L('\n＊「そなたが　つぎのレベルになるには\n　　あと%sポイントの\n　　けいけんがひつようじゃ。🔻')\format(toZenkaku(LevelUps[g_player.level+1].経験値 - g_player.経験値))
						else
							L('\n＊「そなたは　もう　じゅうぶんに\n　　つよい！　なぜに　まだ\n　　りゅうおうを　たおせぬのか？🔻')
						--冒険の書の下りさくじょ
						L('\n＊「では　また　あおう\n　　ゆうしゃ　%sよ！🔻')\format(g_player.name)})\attach()
				else
					MessageBoxRetro({ <index>: coroutine.wrap((_, _)->
						coroutine.yield 1, L('＊「おお　%s！\n　　しんでしまうとは　なにごとだ！🔻\n　　しかも　のろわれているではないか！🔻\n　　のろわれしものよ　でてゆけ！🔻')\format(g_player.name)
						g_player.hp = 1
						Scene.current\detach()
						Scene.current\blackout -> Scene.current = require('DQ1_全体マップ').build_atラダトーム城()
						nil
					)})\attach()
		.traps[] = with {16, 17} --階段
			.talk = =>
				play'assets/DQ1/階段.ogg'
				Scene.current\blackout -> Scene.current = require('DQ1_ラダトーム城').build_from玉座()
		BGM Castle.BGM
		g_player.hp, g_player.mp, g_player.gold = g_player.maxhp, g_player.maxmp, idiv(g_player.gold, 2)
		g_player.x, g_player.y, g_player.direction = 11, 13, {0, -1}


export build_opening = ()->
	with Castle(mapdata)
		.charactors[] = with Charactor(11,12,{0x30}) -- 王様
			.talk = => -- [*] bug 商人のテーブルと共用なので挟んだ先のものも扱えてしまう
				MessageBoxRetro({
					L('＊「おお　%s！\n　　ゆうしゃロトの　ちをひくものよ！\n　　そなたのくるのをまっておったぞ。🔻')\format(g_player.name)
					L'\n＊「その　むかし　ゆうしゃロトが\n　　カミから　ひかりのたまをさずかり\n　　まものたちをふうじこめたという。🔻'
					L'\n＊「しかし　いずこともなくあらわれた\n　　あくまのけしん　りゅうおうが\n　　そのたまを　やみにとざしたのじゃ🔻'
					L'\n＊「このちに　ふたたびへいわをっ！🔻'
					L('\n＊「ゆうしゃ　%sよ！\n　　りゅうおうをたおし　そのてから\n　　ひかりのたまをとりものしてくれ！🔻')\format(g_player.name)
					L'\n＊「わしからの　おくりものじゃ！\n　　そなたのよこにある\n　　はからのはこを　とるがよい！🔻'
					L'\n＊「そして　このへやにいる\n　　へいしにきけば　たびのちしきを\n　　おしえてくれよう。🔻'
					L('\n＊「では　また　あおう\n　　ゆうしゃ　%sよ！🔻')\format(g_player.name)})\attach()
		.charactors[] = 扉(12,16, 'オープニングの扉')
		.charactors[] = 宝箱(12,13, 120) -- 宝箱
		.charactors[] = 宝箱(13,13, L'たいまつ') -- 宝箱
		.charactors[] = 宝箱(14,10, L'かぎ') -- 宝箱
		.charactors[] = with Charactor(11,15,{0x34,0x35}) -- 兵士
			.talk = => MessageBoxRetro({
				L'＊「このしろをでると　となりに\n　　まちが　ある。🔻'
				L'\n＊「そこで　まず　ぶきと　ぼうぐを\n　　かいそろえることだ。🔻'
				L'\n＊「たたかいで　きずついたときは\n　　まちにもどり　やどやにとまると\n　　きずが　かいふくするだろう。🔻'})\attach()
		.charactors[] = with Charactor(13,15,{0x34,0x35}) -- 兵士
			.talk = => MessageBoxRetro({
				L'＊「たからのはこを　ぜんぶとったなら\n　　そのなかに　かぎが　はいっていた\n　　はずだ。🔻'
				L'\n＊「かぎは　１どつかうと　なくなって\n　　しまうが　そのかぎで　とびらを\n　　あけたとき……🔻'
				L'\n＊「おまえのたびが　はじまるだろう。🔻'})\attach()
		.charactors[] = with Charactor(15,13,{0x34,0x35}) -- 兵士
			.update = coroutine.wrap(._update_randomwalk)
			.talk = =>
				sb1 = SelectBoxRetro({L'はい', L'いいえ'})
				MessageBoxRetro({ <index>: coroutine.wrap((_, _)->
					coroutine.yield 1, '＊「ローラひめのことを　ごぞんじか？'
					coroutine.yield 1, sb1\attach()
					if 2 == sb1.index
						coroutine.yield 1, L'\n＊「ひめさまが　まものたちに\n　　さらわれて　はんとしになる…🔻'
						coroutine.yield 1, L'\n＊「おうさまは　なにも\n　　おっしゃらないが\n　　とても　くるしんでいるはず。🔻'
					coroutine.yield 1, L('\n＊「%s！\n　　どうか　ひめを\n　　たすけだして　ください！🔻')\format(g_player.name)
					nil
				)})\attach()
		.traps[] = with {16, 17} --階段
			.talk = =>
				play'assets/DQ1/階段.ogg'
				Scene.current\blackout -> Scene.current = require('DQ1_ラダトーム城').build_from玉座()
		BGM Castle.BGM
		g_player.x, g_player.y = 11, 13
